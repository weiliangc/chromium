# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")
import("//third_party/protobuf/proto_library.gni")

proto_library("media_remoting_proto") {
  proto_out_dir = "media/remoting"
  sources = [
    "proto/remoting_rpc_message.proto",
  ]
}

source_set("rpc") {
  sources = [
    "rpc/proto_enum_utils.cc",
    "rpc/proto_enum_utils.h",
    "rpc/proto_utils.cc",
    "rpc/proto_utils.h",
  ]

  deps = [
    "//base",
  ]

  public_deps = [
    ":media_remoting_proto",
  ]
}

source_set("remoting") {
  sources = [
    "remoting_controller.cc",
    "remoting_controller.h",
    "remoting_renderer_factory.cc",
    "remoting_renderer_factory.h",
  ]
  deps = [
    "//base",
    "//media",
    "//media/mojo/interfaces:remoting",
  ]
}

source_set("media_remoting_tests") {
  testonly = true
  sources = [
    "remoting_controller_unittest.cc",
    "rpc/proto_utils_unittest.cc",
  ]

  deps = [
    ":remoting",
    ":rpc",
    "//base",
    "//base/test:test_support",
    "//media",
    "//media/mojo/interfaces:remoting",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/gfx:test_support",
    "//ui/gfx/geometry",
  ]
}

test("media_remoting_unittests") {
  deps = [
    ":media_remoting_tests",
    "//media/test:run_all_unittests",
  ]
}
